USE [GD2C2020]
GO

-- Creacion de esquema
IF (NOT EXISTS (SELECT * FROM sys.schemas WHERE name = 'NAPOLITANA_CON_FRITAS')) 
BEGIN
    EXEC ('CREATE SCHEMA [NAPOLITANA_CON_FRITAS] AUTHORIZATION [dbo]')
END
GO

-- Dropeo de tablas
IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Item_Compra_Auto')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Item_Compra_Auto
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Item_Compra_Autoparte')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Item_Compra_Autoparte
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Item_Factura_Auto')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Item_Factura_Auto
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Item_Factura_Autoparte')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Item_Factura_Autoparte
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Compra')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Compra
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Factura')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Factura
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Auto')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Auto
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Autoparte')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Autoparte
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Modelo')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Modelo
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Tipo_Transmision')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Tipo_Transmision
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Tipo_Caja')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Tipo_Caja
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Tipo_Auto')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Tipo_Auto
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Fabricante')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Fabricante
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Cliente')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Cliente
GO

IF (EXISTS (SELECT * FROM sys.objects WHERE name = 'Sucursal')) 
	DROP TABLE NAPOLITANA_CON_FRITAS.Sucursal
GO


-- Creacion de estructuras
CREATE TABLE NAPOLITANA_CON_FRITAS.Cliente (
CLIENTE_ID [decimal](18, 0) IDENTITY PRIMARY KEY,
CLIENTE_APELLIDO [nvarchar](255) NULL,
CLIENTE_NOMBRE [nvarchar](255) NULL,
CLIENTE_DIRECCION [nvarchar](255) NULL,
CLIENTE_DNI [decimal](18, 0),
CLIENTE_FECHA_NAC [datetime2](3) NULL,
CLIENTE_MAIL [nvarchar](255) NULL
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Sucursal (
SUCURSAL_ID [decimal](18, 0) PRIMARY KEY,
SUCURSAL_DIRECCION [nvarchar](255) NULL,
SUCURSAL_MAIL [nvarchar](255) NULL,
SUCURSAL_TELEFONO [decimal](18, 0) NULL,
SUCURSAL_CIUDAD [nvarchar](255) NULL
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Tipo_Transmision (
TIPO_TRANSMISION_CODIGO decimal(18,0) PRIMARY KEY,
TIPO_TRANSMISION_DESC nvarchar(255)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Tipo_Caja (
TIPO_CAJA_CODIGO decimal(18,0) PRIMARY KEY,
TIPO_CAJA_DESC nvarchar(255)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Tipo_Auto (
TIPO_AUTO_CODIGO decimal(18,0) PRIMARY KEY,
TIPO_AUTO_DESC nvarchar(255)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Fabricante (
FABRICANTE_ID decimal(18,0) IDENTITY PRIMARY KEY,
FABRICANTE_NOMBRE nvarchar(255) 
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Modelo (
MODELO_CODIGO decimal(18,0) PRIMARY KEY,
MODELO_NOMBRE nvarchar(255),
MODELO_POTENCIA decimal(18,0),
MODELO_FABRICANTE decimal(18,0),
MODELO_TIPO_TRANSMISION decimal(18,0),
MODELO_TIPO_CAJA decimal(18,0),
MODELO_TIPO_MOTOR decimal(18,0),
MODELO_TIPO_AUTO decimal(18,0),
CONSTRAINT FK_MODELO_FABRICANTE FOREIGN KEY (MODELO_FABRICANTE) REFERENCES NAPOLITANA_CON_FRITAS.Fabricante(FABRICANTE_ID),
CONSTRAINT FK_MODELO_TIPO_TRANSMISION FOREIGN KEY (MODELO_TIPO_TRANSMISION) REFERENCES NAPOLITANA_CON_FRITAS.Tipo_Transmision(TIPO_TRANSMISION_CODIGO),
CONSTRAINT FK_MODELO_TIPO_CAJA FOREIGN KEY (MODELO_TIPO_CAJA) REFERENCES NAPOLITANA_CON_FRITAS.Tipo_Caja(TIPO_CAJA_CODIGO),
CONSTRAINT FK_MODELO_TIPO_AUTO FOREIGN KEY (MODELO_TIPO_AUTO) REFERENCES NAPOLITANA_CON_FRITAS.Tipo_Auto(TIPO_AUTO_CODIGO)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Auto (
AUTO_ID decimal(18,0) IDENTITY PRIMARY KEY,
AUTO_NRO_CHASIS nvarchar(50),
AUTO_NRO_MOTOR nvarchar(50),
AUTO_PATENTE nvarchar(50),
AUTO_FECHA_ALTA datetime2(3),
AUTO_CANT_KMS decimal(18,0),
AUTO_MODELO decimal(18,0),
CONSTRAINT FK_AUTO_MODELO FOREIGN KEY (AUTO_MODELO) REFERENCES NAPOLITANA_CON_FRITAS.Modelo(MODELO_CODIGO)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Autoparte (
AUTOPARTE_CODIGO decimal(18,0) PRIMARY KEY,
AUTOPARTE_DESCRIPCION nvarchar(255),
AUTOPARTE_MODELO decimal(18,0),
CONSTRAINT FK_AUTOPARTE_MODELO FOREIGN KEY (AUTOPARTE_MODELO) REFERENCES NAPOLITANA_CON_FRITAS.Modelo(MODELO_CODIGO)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Compra (
COMPRA_NRO decimal(18,0) PRIMARY KEY,
COMPRA_FECHA datetime2(3),
COMPRA_SUCURSAL decimal(18,0),
COMPRA_CLIENTE decimal(18,0),
COMPRA_PRECIO decimal(18,2),
CONSTRAINT FK_COMPRA_SUCURSAL FOREIGN KEY (COMPRA_SUCURSAL) REFERENCES NAPOLITANA_CON_FRITAS.Sucursal(SUCURSAL_ID),
CONSTRAINT FK_COMPRA_CLIENTE FOREIGN KEY (COMPRA_CLIENTE) REFERENCES NAPOLITANA_CON_FRITAS.Cliente(CLIENTE_ID)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Factura (
FACTURA_NRO decimal(18,0) PRIMARY KEY,
FACTURA_FECHA datetime2(3),
FACTURA_SUCURSAL decimal(18,0),
FACTURA_CLIENTE decimal(18,0),
FACTURA_PRECIO decimal(18,2),
CONSTRAINT FK_FACTURA_SUCURSAL FOREIGN KEY (FACTURA_SUCURSAL) REFERENCES NAPOLITANA_CON_FRITAS.Sucursal(SUCURSAL_ID),
CONSTRAINT FK_FACTURA_CLIENTE FOREIGN KEY (FACTURA_CLIENTE) REFERENCES NAPOLITANA_CON_FRITAS.Cliente(CLIENTE_ID)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Item_Compra_Auto (
ITEM_COMPRA_NRO decimal(18,0) NOT NULL,
ITEM_AUTO_ID decimal(18,0),
ITEM_CANTIDAD decimal(18,0) DEFAULT 1,
ITEM_PRECIO decimal(18,2) NOT NULL,
PRIMARY KEY (ITEM_COMPRA_NRO, ITEM_AUTO_ID),
FOREIGN KEY (ITEM_COMPRA_NRO) REFERENCES NAPOLITANA_CON_FRITAS.Compra(COMPRA_NRO),
CONSTRAINT FK_ITEM_COMPRA_AUTO FOREIGN KEY (ITEM_AUTO_ID) REFERENCES NAPOLITANA_CON_FRITAS.Auto(AUTO_ID)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Item_Compra_Autoparte (
ITEM_COMPRA_NRO decimal(18,0) NOT NULL,
ITEM_AUTOPARTE_ID decimal(18,0),
ITEM_CANTIDAD decimal(18,0) DEFAULT 1,
ITEM_PRECIO decimal(18,2) NOT NULL,
PRIMARY KEY (ITEM_COMPRA_NRO, ITEM_AUTOPARTE_ID),
FOREIGN KEY (ITEM_COMPRA_NRO) REFERENCES NAPOLITANA_CON_FRITAS.Compra(COMPRA_NRO),
CONSTRAINT FK_ITEM_COMPRA_AUTOPARTE FOREIGN KEY (ITEM_AUTOPARTE_ID) REFERENCES NAPOLITANA_CON_FRITAS.Autoparte(AUTOPARTE_CODIGO)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Item_Factura_Auto (
ITEM_FACTURA_NRO decimal(18,0) NOT NULL,
ITEM_AUTO_ID decimal(18,0),
ITEM_CANTIDAD decimal(18,0) DEFAULT 1,
ITEM_PRECIO decimal(18,2) NOT NULL,
PRIMARY KEY (ITEM_FACTURA_NRO, ITEM_AUTO_ID),
FOREIGN KEY (ITEM_FACTURA_NRO) REFERENCES NAPOLITANA_CON_FRITAS.Factura(FACTURA_NRO),
CONSTRAINT FK_ITEM_FACTURA_AUTO FOREIGN KEY (ITEM_AUTO_ID) REFERENCES NAPOLITANA_CON_FRITAS.Auto(AUTO_ID)
);

CREATE TABLE NAPOLITANA_CON_FRITAS.Item_Factura_Autoparte (
ITEM_FACTURA_NRO decimal(18,0) NOT NULL,
ITEM_AUTOPARTE_ID decimal(18,0),
ITEM_CANTIDAD decimal(18,0) DEFAULT 1,
ITEM_PRECIO decimal(18,2) NOT NULL,
PRIMARY KEY  (ITEM_FACTURA_NRO, ITEM_AUTOPARTE_ID),
FOREIGN KEY (ITEM_FACTURA_NRO) REFERENCES NAPOLITANA_CON_FRITAS.Factura(FACTURA_NRO),
CONSTRAINT FK_ITEM_FACTURA_AUTOPARTE FOREIGN KEY (ITEM_AUTOPARTE_ID) REFERENCES NAPOLITANA_CON_FRITAS.Autoparte(AUTOPARTE_CODIGO)
);

-- Se cargan datos de la tabla 'cliente'
insert into NAPOLITANA_CON_FRITAS.Cliente(CLIENTE_APELLIDO,CLIENTE_NOMBRE,CLIENTE_DIRECCION,CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL) 
select distinct CLIENTE_APELLIDO,CLIENTE_NOMBRE,CLIENTE_DIRECCION,CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL 
from gd_esquema.Maestra
where CLIENTE_APELLIDO is not null
union
select distinct FAC_CLIENTE_APELLIDO,FAC_CLIENTE_NOMBRE,FAC_CLIENTE_DIRECCION,FAC_CLIENTE_DNI,FAC_CLIENTE_FECHA_NAC,FAC_CLIENTE_MAIL 
from gd_esquema.Maestra m
where FAC_CLIENTE_APELLIDO is not null;

-- Se cargan datos de la tabla 'sucursal'
insert into NAPOLITANA_CON_FRITAS.Sucursal(SUCURSAL_ID,SUCURSAL_DIRECCION,SUCURSAL_MAIL,SUCURSAL_TELEFONO,SUCURSAL_CIUDAD)
select distinct CAST(SUBSTRING(SUCURSAL_MAIL, 12, 1) as numeric), SUCURSAL_DIRECCION, SUCURSAL_MAIL, SUCURSAL_TELEFONO, SUCURSAL_CIUDAD
from gd_esquema.Maestra
where SUCURSAL_MAIL is not null;

-- Se cargan datos de la tabla 'tipo_auto'
insert into NAPOLITANA_CON_FRITAS.Tipo_Auto(TIPO_AUTO_CODIGO,TIPO_AUTO_DESC)
select distinct tipo_auto_codigo, tipo_auto_desc
from gd_esquema.Maestra
where tipo_auto_codigo is not null;

-- Se cargan datos de la tabla 'tipo_transmision'
insert into NAPOLITANA_CON_FRITAS.Tipo_Transmision(TIPO_TRANSMISION_CODIGO,TIPO_TRANSMISION_DESC)
select distinct TIPO_TRANSMISION_CODIGO, TIPO_TRANSMISION_DESC
from gd_esquema.Maestra
where TIPO_TRANSMISION_CODIGO is not null;

-- Se cargan datos de la tabla 'tipo_caja'
insert into NAPOLITANA_CON_FRITAS.Tipo_Caja(TIPO_CAJA_CODIGO,TIPO_CAJA_DESC)
select distinct TIPO_CAJA_CODIGO, TIPO_CAJA_DESC
from gd_esquema.Maestra
where TIPO_CAJA_CODIGO is not null;

-- Se cargan datos de la tabla 'fabricante'
insert into NAPOLITANA_CON_FRITAS.Fabricante(FABRICANTE_NOMBRE)
select distinct FABRICANTE_NOMBRE
from gd_esquema.Maestra
where FABRICANTE_NOMBRE is not null;

-- Se cargan datos de la tabla 'modelo'
insert into NAPOLITANA_CON_FRITAS.Modelo(MODELO_CODIGO,MODELO_NOMBRE,MODELO_FABRICANTE,MODELO_TIPO_TRANSMISION,MODELO_TIPO_CAJA,MODELO_TIPO_MOTOR,MODELO_TIPO_AUTO)
select distinct MODELO_CODIGO,MODELO_NOMBRE,f.fabricante_id,TIPO_TRANSMISION_CODIGO,TIPO_CAJA_CODIGO,TIPO_MOTOR_CODIGO,TIPO_AUTO_CODIGO
from gd_esquema.Maestra m
join NAPOLITANA_CON_FRITAS.Fabricante f on f.FABRICANTE_NOMBRE = m.FABRICANTE_NOMBRE
where MODELO_CODIGO is not null and TIPO_AUTO_CODIGO is not null;

-- Se cargan datos de la tabla 'auto'
insert into NAPOLITANA_CON_FRITAS.Auto(AUTO_NRO_CHASIS,AUTO_NRO_MOTOR,AUTO_PATENTE,AUTO_FECHA_ALTA,AUTO_CANT_KMS,AUTO_MODELO)
select distinct AUTO_NRO_CHASIS,AUTO_NRO_MOTOR,AUTO_PATENTE,AUTO_FECHA_ALTA,AUTO_CANT_KMS,MODELO_CODIGO
from gd_esquema.Maestra
where AUTO_NRO_CHASIS is not null;

-- Se cargan datos de la tabla 'autoparte'
INSERT INTO NAPOLITANA_CON_FRITAS.Autoparte(AUTOPARTE_CODIGO, AUTOPARTE_DESCRIPCION, AUTOPARTE_MODELO)
SELECT DISTINCT AUTO_PARTE_CODIGO, AUTO_PARTE_DESCRIPCION, MODELO_CODIGO
FROM gd_esquema.Maestra
WHERE AUTO_PARTE_CODIGO IS NOT NULL;

-- Se cargan datos de Compras
select 
	CLIENTE_APELLIDO,CLIENTE_DNI,CLIENTE_MAIL,
	COMPRA_NRO,COMPRA_FECHA,COMPRA_CANT,COMPRA_PRECIO,
	CAST(SUBSTRING(SUCURSAL_MAIL, 12, 1) as numeric) AS sucursal, 
	(select a.AUTO_ID from NAPOLITANA_CON_FRITAS.Auto a where a.AUTO_PATENTE = m.AUTO_PATENTE) AS auto_id, 
	AUTO_PARTE_CODIGO AS AUTOPARTE_ID
into #tempCompras
from gd_esquema.Maestra m
where FACTURA_NRO is null;
	
insert into NAPOLITANA_CON_FRITAS.Compra(COMPRA_CLIENTE,COMPRA_NRO,COMPRA_FECHA,COMPRA_PRECIO,COMPRA_SUCURSAL)
select 
	c.CLIENTE_ID cliente,
	COMPRA_NRO,
	COMPRA_FECHA,
	sum(isnull(COMPRA_CANT,1)*COMPRA_PRECIO) precio,
	sucursal
from #tempCompras t
	join NAPOLITANA_CON_FRITAS.Cliente c on c.CLIENTE_DNI = t.CLIENTE_DNI and c.CLIENTE_APELLIDO = t.CLIENTE_APELLIDO and c.CLIENTE_MAIL = t.CLIENTE_MAIL
group by CLIENTE_ID,COMPRA_NRO,COMPRA_FECHA,sucursal;

-- Se cargan datos de la tabla 'Compra_Auto'
INSERT INTO NAPOLITANA_CON_FRITAS.Item_Compra_Auto (ITEM_COMPRA_NRO, ITEM_AUTO_ID, ITEM_CANTIDAD, ITEM_PRECIO)
SELECT COMPRA_NRO, AUTO_ID, sum(ISNULL(COMPRA_CANT,1)), COMPRA_PRECIO
FROM #tempCompras
WHERE AUTO_ID IS NOT NULL
GROUP BY COMPRA_NRO,AUTO_ID,COMPRA_PRECIO;

-- Se cargan datos de la tabla 'Compra_Autoparte'
INSERT INTO NAPOLITANA_CON_FRITAS.Item_Compra_Autoparte (ITEM_COMPRA_NRO, ITEM_AUTOPARTE_ID, ITEM_CANTIDAD, ITEM_PRECIO)
SELECT COMPRA_NRO, AUTOPARTE_ID, SUM(COMPRA_CANT), COMPRA_PRECIO
FROM #tempCompras
WHERE AUTOPARTE_ID IS NOT NULL
GROUP BY COMPRA_NRO,AUTOPARTE_ID,COMPRA_PRECIO;

drop table #tempCompras;

-- Se cargan datos de la tabla 'factura'
INSERT INTO NAPOLITANA_CON_FRITAS.Factura(FACTURA_NRO, FACTURA_FECHA, FACTURA_SUCURSAL, FACTURA_CLIENTE, FACTURA_PRECIO)
SELECT DISTINCT M.FACTURA_NRO, M.FACTURA_FECHA, CAST(SUBSTRING(M.FAC_SUCURSAL_MAIL,12,1) AS NUMERIC), C.CLIENTE_ID, NULL 
FROM gd_esquema.Maestra M
JOIN NAPOLITANA_CON_FRITAS.Cliente C ON C.CLIENTE_DNI = M.FAC_CLIENTE_DNI AND C.CLIENTE_APELLIDO = M.FAC_CLIENTE_APELLIDO AND C.CLIENTE_NOMBRE = M.FAC_CLIENTE_NOMBRE
	AND C.CLIENTE_DIRECCION = M.FAC_CLIENTE_DIRECCION AND C.CLIENTE_FECHA_NAC = M.FAC_CLIENTE_FECHA_NAC
	AND C.CLIENTE_MAIL = M.FAC_CLIENTE_MAIL
WHERE FACTURA_NRO IS NOT NULL;

-- Se cargan datos de la tabla 'Item_Factura_Auto'
INSERT INTO NAPOLITANA_CON_FRITAS.Item_Factura_Auto(ITEM_FACTURA_NRO, ITEM_AUTO_ID, ITEM_PRECIO)
SELECT DISTINCT M.FACTURA_NRO, A.AUTO_ID, M.PRECIO_FACTURADO
FROM gd_esquema.Maestra M
JOIN NAPOLITANA_CON_FRITAS.Auto A ON A.AUTO_NRO_CHASIS = M.AUTO_NRO_CHASIS AND A.AUTO_NRO_MOTOR = M.AUTO_NRO_MOTOR 
    AND A.AUTO_PATENTE = M.AUTO_PATENTE AND A.AUTO_FECHA_ALTA = M.AUTO_FECHA_ALTA 
    AND A.AUTO_CANT_KMS = M.AUTO_CANT_KMS AND A.AUTO_MODELO = M.MODELO_CODIGO
WHERE M.FACTURA_NRO IS NOT NULL;

-- Se cargan datos de la tabla 'Item_Factura_Autoparte'
INSERT INTO NAPOLITANA_CON_FRITAS.Item_Factura_Autoparte(ITEM_FACTURA_NRO, ITEM_AUTOPARTE_ID, ITEM_CANTIDAD, ITEM_PRECIO)
SELECT DISTINCT FACTURA_NRO, AUTO_PARTE_CODIGO, SUM(CANT_FACTURADA), PRECIO_FACTURADO
FROM gd_esquema.Maestra
WHERE FACTURA_NRO IS NOT NULL AND AUTO_PARTE_CODIGO IS NOT NULL
GROUP BY FACTURA_NRO, AUTO_PARTE_CODIGO, PRECIO_FACTURADO;

-- Update precio total de facturas
CREATE TABLE #PrecioFacturaTemp (
	FACTURA_NRO DECIMAL(18,0),
	PRECIO DECIMAL(18,2)
)

INSERT INTO #PrecioFacturaTemp
SELECT T.ITEM_FACTURA_NRO, SUM(T.PrecioItem)
FROM (SELECT ITEM_FACTURA_NRO, ITEM_CANTIDAD * ITEM_PRECIO AS PrecioItem FROM NAPOLITANA_CON_FRITAS.Item_Factura_Auto) T
GROUP BY T.ITEM_FACTURA_NRO;

INSERT INTO #PrecioFacturaTemp
SELECT ITEM_FACTURA_NRO, SUM(T.PrecioItem)
FROM (SELECT ITEM_FACTURA_NRO, ITEM_CANTIDAD * ITEM_PRECIO AS PrecioItem FROM NAPOLITANA_CON_FRITAS.Item_Factura_Autoparte) T
GROUP BY T.ITEM_FACTURA_NRO;


UPDATE NAPOLITANA_CON_FRITAS.Factura
SET FACTURA_PRECIO = T.Precio
FROM NAPOLITANA_CON_FRITAS.Factura F
JOIN #PrecioFacturaTemp T ON T.FACTURA_NRO = F.FACTURA_NRO;

DROP TABLE #PrecioFacturaTemp;
